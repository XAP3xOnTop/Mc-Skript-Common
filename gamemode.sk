function switchGM(g: gamemode, s: sender, p: string = "0"):
    if {_s} has permission "gamemode.%{_g}%" or "gamemode.*":
        if {_p} is "0":
            if {_s} is player:
                set {_s}'s gamemode to {_g}
                send parseMsg("gamemodeChange", {_s}, "%{_g}%") to {_s}
            else:
                send parseMsg("notPlayer", {_s}) to {_s}
        else:
            if {_p} parsed as offline player is online:
                set {_p} parsed as player's gamemode to {_g}
                send parseMsg("gamemodeChangeFor", {_p} parsed as offline player, "%{_g}%") to {_s}
            else:
                send parseMsg("notOnline", {_p} parsed as offline player) to {_s}
    else:
        send parseMsg("noPermissions", {_s}) to {_s}

command /gmc [<offline player>]:
    aliases: /gm1, /gamemodec, /creative
    trigger:
        if arg-1 is set:
            switchGM(creative, sender, "%arg-1%")
        else:
            switchGM(creative, sender)

command /gms [<offline player>]:
    aliases: /gm0, /gamemodes, /survival
    trigger:
        if arg-1 is set:
            switchGM(survival, sender, "%arg-1%")
        else:
            switchGM(survival, sender)

command /gma [<offline player>]:
    aliases: /gm2, /gamemodea, /adventure
    trigger:
        if arg-1 is set:
            switchGM(adventure, sender, "%arg-1%")
        else:
            switchGM(adventure, sender)

command /gmsp [<offline player>]:
    aliases: /gm3, /gamemodesp, /spectator
    trigger:
        if arg-1 is set:
            switchGM(spectator, sender, "%arg-1%")
        else:
            switchGM(spectator, sender)

command /gamemode [<text>] [<offline player>]:
    aliases: /gm
    trigger:
        if arg-1 is set:
            if arg-1 is "survival", "creative", "adventure" or "spectator":
                set {_temp} to arg-1
            else if arg-1 is "0", "1", "2", "3", "s", "c", "a" or "sp":
                set {_temp} to arg-1
                if arg-1 is "0" or "s":
                    set {_temp} to "survival"
                else if arg-1 is "1" or "c":
                    set {_temp} to "creative"
                else if arg-1 is "2" or "a":
                    set {_temp} to "adventure"
                else if arg-1 is "3" or "sp":
                    set {_temp} to "spectator"
            else:
                send parseMsg("invalidGM", sender) to sender
                stop
            if arg-2 is set:
                switchGM({_temp} parsed as gamemode, sender, "%arg-2%")
            else:
                switchGM({_temp} parsed as gamemode, sender)
        else:
            send parseMsg("invalidGM", sender) to sender